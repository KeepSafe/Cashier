apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  defaultConfig {
    minSdkVersion versions.minSdk
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  testOptions {
    unitTests.all {
      testLogging {
        exceptionFormat 'full'
        showStackTraces true
        showCauses true
        events "passed", "skipped", "failed", "standardError"
      }
    }
  }
}

dependencies {
  compileOnly deps.autoValue
  compileOnly deps.supportAnnotations
  annotationProcessor deps.autoValue
  annotationProcessor deps.autoParcel

  testImplementation deps.robolectric
  testImplementation deps.junit
  testImplementation deps.mockito
  testImplementation deps.truth
}

install {
  repositories.mavenInstaller {
    pom.project {
      name libname
      description project.description
      url "https://github.com/KeepSafe/$libname"
      inceptionYear 2016

      packaging 'aar'
      groupId project.group
      artifactId 'cashier'
      version project.version

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      scm {
        connection "https://github.com/KeepSafe/${libname}.git"
        url "https://github.com/KeepSafe/$libname"
      }
      developers {
        developer {
          name 'Keepsafe'
        }
      }
    }
  }
}

apply from: rootProject.file('bintray.gradle')
apply from: rootProject.file('javadoc.gradle')
